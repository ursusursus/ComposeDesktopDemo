import org.jetbrains.compose.desktop.application.dsl.TargetFormat

plugins {
    id "org.jetbrains.kotlin.jvm" version "1.8.0"
    id "org.jetbrains.compose" version "1.3.0"
    id "com.google.devtools.ksp" version "1.8.0-1.0.9"
    id "com.squareup.sqldelight" version "1.5.4"
}

repositories {
    google()
    mavenCentral()
}

kotlin {
    jvmToolchain(11)
}

compose.desktop {
    application {
        mainClass = "MainKt"
        nativeDistributions {
            targetFormats(TargetFormat.Dmg, TargetFormat.Msi, TargetFormat.Deb)
            packageName = "DesktopDemo"
            packageVersion = "1.0.0"
        }
    }
}

sqldelight {
    AppDatabase {
        packageName = "sk.ursus.demo.db"
    }
}

dependencies {
    implementation "com.squareup.sqldelight:sqlite-driver:1.5.4"
    implementation "com.squareup.sqldelight:coroutines-extensions:1.5.4"

    ksp "com.squareup.moshi:moshi-kotlin-codegen:1.14.0"
    implementation "com.squareup.moshi:moshi:1.14.0"

    implementation "com.squareup.retrofit2:converter-moshi:2.9.0"
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation "com.squareup.okhttp3:okhttp:4.10.0"

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-jvm:1.6.4"
    implementation "org.jetbrains.compose.desktop:desktop-jvm-macos-arm64:1.3.0"
}
